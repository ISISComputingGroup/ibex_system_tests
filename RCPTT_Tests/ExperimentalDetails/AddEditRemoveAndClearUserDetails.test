--- RCPTT testcase ---
Format-Version: 1.0
Element-Name: AddEditRemoveAndClearUserDetails
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _G9wcgEUCEea3c7YIX-7B3A
Runtime-Version: 2.1.0.201606221726
Save-Time: 7/31/17 12:08 PM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

switch_to_experiment_details_view

let [val view_name "My View"] [val def_user "DEFAULT_USER"] {
	
	/*
	*	Clear and assert dashboard empty
	*/
	clear_and_verify $view_name
	
	/*
	*	Add three default users and assert dashboard
	*/
	try -times 10 -delay 100 -command {
		try -command {
			with [get-view $view_name] {
			    with [get-button Add] {
			        click
			        click
			        click
			        click
			    }
			}
			
			test_dashboard [concat $def_user ", " $def_user "_1, " $def_user "_2 and " $def_user "_3"]
			
		} -catch {
			clear_and_verify $view_name
			verify-true False
		}
	}
	
	/*
	*	Remove single user and assert dashboard
	*/
	try -times 10 -delay 100 -command {
		get-view $view_name | remove_user [concat $def_user "_1"]
		test_dashboard [concat $def_user ", " $def_user "_2 and " $def_user "_3"]
	}
	
	/*
	*	Remove multiple user and assert dashboard
	*/
	try -times 10 -delay 100 -command { 
		get-view $view_name | remove_user [concat $def_user "_\d"]
		test_dashboard $def_user
	}
	
	/*
	*	Edit user, set and assert dashboard
	*/
	let [val new_name "TEST"] {
		try -times 10 -delay 100 -command {
			clear_and_verify $view_name
			with [get-view $view_name] {
				get-button Add | click
				edit_user $def_user $new_name
			    get-button Set -after [get-label "Experiment Team"] | click
			}
			test_dashboard $new_name
			clear_and_verify $view_name
		}
	}
	
	let [val new_name "TEST"] [val org1 "TEST_ORG"] [val org2 "NOT_TEST_ORG"] {
		/*
		* Users with the same name and different organisations are treated seperately
		*/
		try -times 10 -delay 100 -command {
			clear_and_verify $view_name
			with [get-view $view_name] {
			    get-button Add | click
			    with [get-table] {
			        edit_user $def_user $new_name
			        edit_org $new_name $org1
			    }
			    get-button Add | click
			    with [get-table] {
			    	edit_org $def_user $org2
					edit_user $def_user $new_name
			    }
			    get-button Set -after [get-label "Experiment Team"] | click
			}
			test_dashboard [concat $new_name " and " $new_name]
		}
		
		/*
		* Users with the same name and organisation get amalgamated
		*/
		with [get-view $view_name] {
		    with [get-table] {
				edit_org $new_name $org2
		    }
		    get-button Set -after [get-label "Experiment Team"] | click
		}
		test_dashboard $new_name
		clear_and_verify $view_name
	}
}
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
