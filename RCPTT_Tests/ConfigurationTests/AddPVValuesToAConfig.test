--- RCPTT testcase ---
Format-Version: 1.0
Element-Name: AddPVValuesToAConfig
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _RgALAMdeEea9WqmIdIbeTA
Runtime-Version: 2.1.0.201606221726
Save-Time: 4/5/17 1:30 PM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

proc "select_from_table" [val table -input true] [val pv01] {
	with $table {
		select pv01
		get-item pv01 | get-property selected | equals true | verify-true
	}
}

proc "table_populated"[val table -input true] [val pv01] [val val01] {
	with $table {
		get-item pv01 | get-property "values['Name']" | equals pv01 | verify-true
    	get-item pv01 | get-property "values['Value']" | equals val01 | verify-true
	}
}

// Create variables for one config, pv address and value
let [val config01 [test_prefix "config01"]] [val pv01 "pv01"] [val val01 "val01"]{
	// In a new configuration...
	with [new_config] {
	    get-tab-folder | get-tab-item "IOC PV Values" | click
	    try -times 10 -delay 100 -command {
	    	get-button Remove | get-property enablement | equals false | verify-true
	        get-button Add | check_button_enabled_and_click
	    }
    	with [get-group "Selected PV"] {
        	get-editbox -after [get-label Name] | set-text pv01
        	get-editbox -after [get-label Value] | set-text val01
    	}
    	with [get-table] {
    		table_populated pv01 val01
    		select_from_table pv01
    	}
    	
    	get-button Remove | get-property enablement | equals true | verify-true
    	
    	with [get-table] {
    		// Ensure that not every key deletes items
    		select_from_table pv01
    		
    		type-text a

    		table_populated pv01 val01
    		
    		select_from_table pv01
    		
    		key-type Del
    		
    		get-property itemCount | equals 0 | verify-true
    	}
    }
}
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
