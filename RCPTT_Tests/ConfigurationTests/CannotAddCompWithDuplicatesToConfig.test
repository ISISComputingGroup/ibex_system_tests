--- RCPTT testcase ---
Format-Version: 1.0
Element-Name: CannotAddCompWithDuplicatesToConfig
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _6DleABkZEeeBk98u4J3lxA
Runtime-Version: 2.1.0.201606221726
Save-Time: 4/4/17 5:14 PM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

// Create variables for config, component and block names
let [val config01 [test_prefix "config01"]] [val comp01 [test_prefix "comp01"]] [val block_dup "duplicate_block"] [val block_rn "renamed_block"] {
	// Create configuration with block
	with [new_config] {
		add_block $block_dup $block_dup
		save_and_name_config $config01
	}
	assert_wait_for_dialog_closed
	
	// Create component with duplicate block
	with [new_comp] {
		add_block $block_dup $block_dup
		save_and_name_comp $comp01
	}
	assert_wait_for_dialog_closed
	
	// Try to add comp to config
	with [edit_config $config01] {
		add_comp $comp01
		
		// Verify error
		with [get-window "Conflicts with current configuration"] {
			 get-property "isVisible()" | equals true | verify-true
			 get-button Ok | check_button_enabled_and_click
		}
		get-button Cancel | click
	}
	
	// Rename block in comp to remove duplicate
	with [edit_comp $comp01] {
	    get-tab-folder | get-tab-item Blocks | click
	    get-table | select_in_table $block_dup
	    get-button "Edit Block" | click
		with [get-window "Block Configuration"] {
			with [get-group "Selected block"] {
				get-editbox -after [get-label "Name:"] | set-text $block_rn
			}
			get-button OK | check_button_enabled_and_click
		}
		save_comp
	}
	assert_wait_for_dialog_closed
	
	// Try to add comp to config
	with [edit_config $config01] {
		add_comp $comp01
		save_config
	}
	assert_wait_for_dialog_closed
	
	// Assert both blocks in config
	with [edit_config $config01] {
	    get-tab-folder | get-tab-item Blocks | click
	    get-table | get-property itemCount | equals 2 | verify-true
	}
}
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
