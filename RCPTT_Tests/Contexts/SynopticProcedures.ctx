--- RCPTT testcase ---
Format-Version: 1.0
Context-Type: org.eclipse.rcptt.ctx.ecl
Element-Name: SynopticProcedures
Element-Type: context
Element-Version: 2.0
Id: _FsR8cEm2Eeamkst2qvREOQ
Runtime-Version: 2.0.1.201508250612
Save-Time: 7/21/16 2:11 PM
------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998
Content-Type: text/ecl
Entry-Name: .ecl.context

// Open the "New synoptic" window
proc "new_synoptic" {
	try -times 100 -delay 200 -command {
		get-menu "Synoptic/New" | click
	}
	get-window "New Synoptic"
}

// Open the "Edit Synoptic" window
proc "edit_synoptic" {
	try -times 100 -delay 200 -command {
		get-menu "Synoptic/Edit" | click
	}
	get-window "Edit Synoptic"
}

// Save a synoptic (using the "save" dialog" on an edited synoptic) 
proc "save_synoptic" {
	try -times 100 -delay 200 -command {
		with [get-window "Edit Synoptic"] {
			get-button Save | check_button_enabled_and_click
		}
	}
}

// Save a new synoptic, naming it in the process (using the "save as" dialog on a new OR edited synoptic)
proc "save_and_name_synoptic" [val name]  [val window -input true ] {
	try -times 100 -delay 200 -command {
		with $window {
	    	get-button "Save as ..." | click
	    	with [get-window "Save Synoptic As"] {
				try {
				    get-editbox -after [get-label "Name:"] | set-text $name
				    get-button OK | check_button_enabled_and_click
			    } -catch {
			    	cancel_dialog
			    }
			}
		}
	}
}

// Delete a synoptic
proc "delete_synoptic" [val name] {
	try -times 100 -delay 200 -command {
		get-menu "Synoptic/Delete" | click
		with [get-window "Delete Synoptic"] {
			try {
				get-list | select_in_list $name
		    	get-button OK | check_button_enabled_and_click
		    } -catch {
		    	cancel_dialog
		    }
		}
	}
}

// Add a component to a synoptic (e.g. "BEAMSTOP", "CHOPPER", "DAE", etc.)
proc "add_component" [val component_name] [val window -input true ] {
	with $window {
	    get-button "Add Component" | click
	    get-combo -after [get-label Type] | select $component_name
	    get-editbox -after [get-label Name] | set-text $component_name
	}
}

// Open a synoptic to view (in the "Synoptic" perspective)
proc "view_synoptic" [val synoptic] {
	try -times 100 -delay 200 -command {
		switch_to_synoptic_view
		with [get-view "Synoptic navigator"] {
    		with [get-group "Synoptic Selection" | get-combo] {
    			select $synoptic
    			get-property selection | equals $synoptic | verify-true
    		}
    	}
	}
}
------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998--
