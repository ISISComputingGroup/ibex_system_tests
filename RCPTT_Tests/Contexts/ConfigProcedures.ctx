--- RCPTT testcase ---
Format-Version: 1.0
Context-Type: org.eclipse.rcptt.ctx.ecl
Element-Name: ConfigProcedures
Element-Type: context
Element-Version: 2.0
Id: _oJdLMEdZEeakK_49We-6tQ
Runtime-Version: 2.0.1.201508250612
Save-Time: 7/21/16 2:10 PM

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998
Content-Type: text/ecl
Entry-Name: .ecl.context

// Open the "New configuration" window
proc "new_config" {
	try -times 100 -delay 200 -command {
		get-menu "Configuration/Configurations/New" | click
	} 
	get-window "New configuration"
}

// Open the "Edit configuration" window
proc "edit_config" {
	try -times 100 -delay 200 -command {
		get-menu "Configuration/Configurations/Edit" | click
	} 
	get-window "Edit Configuration"
}

// Open the "Edit current configuration" window
proc "edit_current_config" {
	try -times 100 -delay 200 -command {
		get-menu "Configuration/Edit Current Configuration..." | click
	} 
	get-window "Edit Configuration"
}
	
// Set a description name for a configuration (in the new OR edit configuration window)
proc "set_config_description" [val description] [val window -input true ] {
	try -times 100 -delay 200 -command {
		with $window {
        	get-tab-folder | get-tab-item Summary | click
    		get-group Summary | get-editbox -after [get-label "Description:"] | set-text $description
	   	}
	}
}

// Save a configuration (using the "save" dialog on an edited configuration)
proc "save_config" {
	try -times 100 -delay 200 -command {
		with [get-window "Edit Configuration"] {
			get-button Save| check_button_enabled_and_click
		}
	}
}

// Save a new configuration, naming it in the process (using the "save as" dialog on a new OR edited configuration)
proc "save_and_name_config" [val name]  [val window -input true ] {
	try -times 100 -delay 200 -command {
		with $window {
	    	get-button "Save as ..." | click
	    }
	    with [get-window "Save Configuration As"] {
			try {	
	        	get-editbox -after [get-label "Name:"] | set-text $name
	        	get-button OK | check_button_enabled_and_click
	    	} -catch {
	    		cancel_dialog
	    	}
	    }
	}
}

// Load a configuration
proc "load_config" [val name] {
	try -times 100 -delay 200 -command {
		get-menu "Configuration/Configurations/Load" | click
		with [get-window "Load Configuration"] {
			try {
	    		get-list | select_in_list $name
	    		get-button OK | check_button_enabled_and_click
	    	} -catch {
	    		cancel_dialog
	    	}
		}
	}
}

// Delete a configuration
proc "delete_config" [val name] {
	try -times 100 -delay 200 -command {
		get-menu "Configuration/Configurations/Delete" | click
		with [get-window "Delete Configurations"] {
			try {
			    get-list | select_in_list $name
			    get-button OK | check_button_enabled_and_click
			} -catch {
				cancel_dialog
			}
		}
	}
}

// Add a block (to a new OR edited configuration)
proc "add_block"[val block_name] [val pv_name] [val window -input true ] {
	try -times 100 -delay 200 -command {
		with $window {
			get-tab-folder | get-tab-item Blocks | click
			get-button "Add Block" | click
			with [get-window "Block Configuration"] {
				try {
				    with [get-group "Selected block"] {
				        get-editbox -after [get-label "Name:"] | set-text $block_name
				        get-editbox -after [get-label "PV address:"] | set-text $pv_name
				    }
					get-button OK | check_button_enabled_and_click
				} -catch {
					cancel_dialog
				}
			}
		}
	}
}

// Delete all blocks?

// Add a group
proc "add_group" [val window -input true ] {
	try -times 100 -delay 200 -command {
		with $window {
			get-tab-folder | get-tab-item Groups | click
	    	get-group Groups | get-button Add | click
		}
	}
}

// Transfer a block to a group
proc "add_block_to_group" [val group_name] [val block_name] [val window -input true ] {
	try -times 100 -delay 200 -command {
		with $window {
			get-tab-folder | get-tab-item Groups | click
			get-group Groups | get-list | select_in_list $group_name
			with [get-group "Selected group" | get-group Blocks] {
				get-list | select_in_list $block_name
				get-button | click
			}
		}
	}
}

//Create a configuration with nothing but a description
proc "create_empty_config" [val config_name] {
	with [new_config] {
		set_config_description $config_name
		save_and_name_config $config_name
	}
}

//Create a configuration with one block
proc "create_simple_config_and_load" [val config_name] [val block_name] [val pv] {
	// In a new configuration...
	with [new_config] {
		// Add a block, set a description and save the configuration
		add_block $block_name $pv
		set_config_description $config_name
		save_and_name_config $config_name
	}
	
	// Wait is needed to ensure that the load configuration dialog is fully updated
	wait 10000
	
	// Load the configuration previously saved
	load_config $config_name
}

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998--
