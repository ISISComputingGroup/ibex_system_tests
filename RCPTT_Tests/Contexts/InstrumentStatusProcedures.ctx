--- RCPTT testcase ---
Format-Version: 1.0
Context-Type: org.eclipse.rcptt.ctx.ecl
Element-Name: InstrumentStatusProcedures
Element-Type: context
Element-Version: 2.0
Id: _u7zXkB5uEeaqQYS4M76Ebw
Runtime-Version: 2.1.0.201606221726
Save-Time: 8/9/17 1:04 PM

------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998
Content-Type: text/ecl
Entry-Name: .ecl.context

proc "assert_instrument_is" [val text] {
	try -times 50 -delay 200 -command {
	    get-view Dashboard | get-control Any -index 1 | get-property "getChildren().Control[0].getText()" 
	        | contains $text | verify-true
	}
}

proc "assert_instrument_is_setup" {
	assert_instrument_is "SETUP"
}

proc "assert_instrument_is_running" {
	assert_instrument_is "RUNNING"
}

proc "assert_instrument_is_paused" {
	assert_instrument_is "PAUSED"
}

proc "assert_instrument_is_unknown" {
	assert_instrument_is "UNKNOWN"
}

proc "click_DAE_run_control" [val button_text] {
	switch_to_dae_view
	
	with [get-view "DAE Control Program"] {
	    get-tab-folder | get-tab-item "Run Summary" | click
	    get-button $button_text | click
	}	
}

proc "begin_run" {
    click_DAE_run_control "BEGIN RUN"	
	assert_instrument_is_running
}

proc "end_run" {
	click_DAE_run_control "END RUN"
	assert_instrument_is_setup
}

proc "wait_for_dialog_exists" {
	try -command {
		get-window -class WaitForDialog
		bool true
	} -catch {
		bool false
	}
}

proc "assert_wait_for_dialog_closed" {
	wait 3000 //Dialog takes some time to display

    // This should be an excessive amount of time. Lower once ticket #2526 is fixed
	try -times 600 -delay 1000 -command {
		if [wait_for_dialog_exists] {
			bool true | verify-false
		}
	}
}
------=_.ecl.context-718f04b4-ed39-33e3-af62-0995e4561998--
