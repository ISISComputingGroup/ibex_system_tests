--- RCPTT testcase ---
Format-Version: 1.0
Element-Name: WhenComponetGroupRemovedItIsRemovedFromConfig
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _bpiCAEzwEee1irVDJO9Pmw
Runtime-Version: 2.1.0.201606221726
Save-Time: 6/9/17 10:57 AM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

let [val config_name [test_prefix "group_test_config"]] [val comp_name [test_prefix "group_test_comp"]] [val config_block "config_block" ] [val comp_block_1 "comp_block_1"] 
	[val comp_block_2 "comp_block_2"] [val config_group "CONFIG_GROUP"] [val comp_group_1 "COMP_GROUP_1"] [val comp_group_2 "COMP_GROUP_2"] [val comp_group_3 "COMP_GROUP_3"]
	[val other_group "Other"]  {
	
	create_simple_config_tree_with_groups $config_name $comp_name $config_block $comp_block_1 $comp_block_2 $config_group $comp_group_1 $comp_group_2
	
	// Do some reordering of groups in the config
	with [edit_current_config] {
	   get-tab-folder | get-tab-item Groups | click
	      
       // Move comp_group_1 up 1
       get-list | select $comp_group_1
       get-button | click
        
       // Move comp_group_2 up 1
       get-list | select $comp_group_2
       get-button | click
        
       save_config
    }
	
	// Remove a group from the component
	with [edit_comp $comp_name] {
		remove_group $comp_group_2
	}
	save_comp
	
	// Edit the config. Check it has 1 config and 1 component group, and that the component group can't be edited
	with [edit_current_config] {
		get-tab-folder | get-tab-item Groups | check_enabled_and_click
		with [get-group Groups | get-list] {
	    	get-property "getItemCount()" | equals 2 | verify-true
    		get-property "getItems().String[0]" | equals $comp_group_1 | verify-true
    		get-property "getItems().String[1]" | equals $config_group | verify-true
    		select $comp_group_1
    	}
    	get-group "Selected group" | get-editbox -after [get-label "Name:"] | get-property enablement | equals false | verify-true
    	get-button Cancel | click
    }
}
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
