--- RCPTT testcase ---
Format-Version: 1.0
Element-Name: CanPauseEndAbortCancelResumeARun
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _qNn_cFDTEeen85wkhybD2Q
Runtime-Version: 2.1.0.201606221726
Save-Time: 8/9/17 11:58 AM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

proc "check_pause_state" {    
	assert_instrument_is "PAUSED"
	// In PAUSED only end, resume and abort should be enabled
	try -times 10 -delay 200 - command {
    	get-button "BEGIN RUN" | get-property enablement | equals false| verify-true
    	get-button "CANCEL ABORT" | get-property enablement | equals false | verify-true
    	get-button "PAUSE RUN" | get-property enablement | equals false | verify-true
    	get-button "RESUME RUN" | get-property enablement | equals true | verify-true
    	get-button "ABORT RUN" | get-property enablement | equals true | verify-true
    	get-button "END RUN" | get-property enablement | equals true | verify-true
    }
}

proc "check_setup_state" {    
	assert_instrument_is "SETUP"
    // In SETUP only begin and cancel abort are enabled
	try -times 10 -delay 200 - command {
    	get-button "BEGIN RUN" | get-property enablement | equals true | verify-true
    	get-button "CANCEL ABORT" | get-property enablement | equals true | verify-true
    	get-button "PAUSE RUN" | get-property enablement | equals false | verify-true
    	get-button "RESUME RUN" | get-property enablement | equals false | verify-true
    	get-button "ABORT RUN" | get-property enablement | equals false | verify-true
    	get-button "END RUN" | get-property enablement | equals false | verify-true
    }
}

proc "check_running_state" {    
	assert_instrument_is "RUNNING"
    // In RUNNING only end, pause and abort should be enabled
	try -times 10 -delay 200 - command {
    	get-button "BEGIN RUN" | get-property enablement | equals false| verify-true
    	get-button "CANCEL ABORT" | get-property enablement | equals false | verify-true
    	get-button "PAUSE RUN" | get-property enablement | equals true | verify-true
    	get-button "RESUME RUN" | get-property enablement | equals false | verify-true
    	get-button "ABORT RUN" | get-property enablement | equals true | verify-true
    	get-button "END RUN" | get-property enablement | equals true | verify-true
    }
}

assert_instrument_is_setup
configure_dae_for_tests
switch_to_dae_view

with [get-view "DAE Control Program"] {
	check_setup_state
    
    // Begin run
    get-button "BEGIN RUN" | click
    check_running_state
    
    // Pause run
    get-button "PAUSE RUN" | click
    check_pause_state
    
    // Resume run
    get-button "RESUME RUN" | click
    check_running_state
    
 	// Abort run
    get-button "ABORT RUN" | click
    check_setup_state
    
    // Cancel abort
    get-button "CANCEL ABORT" | click
    check_pause_state
    
    // Resume from paused after cancel abort
    get-button "RESUME RUN" | click
    check_running_state
    
    // End run
    get-button "END RUN" | click
    check_setup_state
}

// Make sure we leave things as we found them    
assert_instrument_is_setup
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
