--- RCPTT testcase ---
Format-Version: 1.0
Element-Name: ComponentsCanBeCopiedAndDeleted
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _qmBtAFECEeeZA_wPu-VRqQ
Runtime-Version: 2.1.0.201606221726
Save-Time: 6/14/17 4:55 PM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

let [val beamstop "BEAMSTOP"] [val dae "DAE"] {
	with [new_synoptic] {
		// Create a new synoptic with two components
		add_component $beamstop
		add_component $dae
	
		// Copy single component
		select_single_component $beamstop
		get-button "Copy Component" | check_enabled_and_click
		
		// Assert one new component and two components named beamstop
		with [get-tree] {
			get-property itemCount | equals 3 | verify-true
    		get-property "getItems().TreeItem[0].getText()" | contains $beamstop | verify-true
    		get-property "getItems().TreeItem[1].getText()" | contains $beamstop | verify-true
    	}
	
		// Assert cannot copy multiple components of different type
		get-tree | select $beamstop $dae
		get-button "Copy Component" | is-disabled | verify-true
	
		// Copy multiple components
		get-tree | select $beamstop [concat $beamstop " (1)"]
		get-button "Copy Component" | check_enabled_and_click
	
		// Assert two new component and four components named beamstop
		with [get-tree] {
			get-property itemCount | equals 5 | verify-true
    		get-property "getItems().TreeItem[0].getText()" | contains $beamstop | verify-true
    		get-property "getItems().TreeItem[1].getText()" | contains $beamstop | verify-true
    		get-property "getItems().TreeItem[2].getText()" | contains $beamstop | verify-true
    		get-property "getItems().TreeItem[3].getText()" | contains $beamstop | verify-true
    	}	
	
		// Delete single component
		delete_component [concat $beamstop " (3)"]
		
		// Assert one less component
		get-tree | get-property itemCount | equals 4 | verify-true
	
		//Delete multiple components
		get-tree | select [concat $beamstop " (1)"] [concat $beamstop " (2)"]
		delete_selected_component
		
		// Assert back where we started
		with [get-tree] {
			get-property itemCount | equals 2 | verify-true
    		get-property "getItems().TreeItem[0].getText()" | equals $beamstop | verify-true
    		get-property "getItems().TreeItem[1].getText()" | equals $dae | verify-true
    	}	
	}	
}

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
