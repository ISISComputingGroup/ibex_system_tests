--- RCPTT testcase ---
Format-Version: 1.0
Element-Name: SynopticCannotHaveMultipleComponentsWithSameName
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _lFSlQNy3EeaGu9vK5vllnA
Runtime-Version: 2.1.0.201606221726
Save-Time: 10/19/17 9:50 AM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

proc "error_text" {
	//Error text box seems to be potentially placed in two different locations, check both
	try -command {
		get-editbox -after [get-label Description -after [get-label Value]] -index 1 | get-property text
	} -catch {
		get-editbox -after [get-label "Select a Component to view/edit details"] | get-property text
	}
}

proc "save_enabled" {
	get-button "Save as ..." | get-property enablement | equals true
}

let [val duplicate1 "Eurotherm"] [val duplicate2 "Kepco"]{

	with [new_synoptic] {
		
		//Test that duplicates on same level produces error
		add_component "Temperature controllers" $duplicate1
		add_component "Temperature controllers" $duplicate1

		error_text | contains $duplicate1 | verify-true
		save_enabled | verify-false
		
		//Test that removing duplicate clears error
		delete_component $duplicate1
		error_text | equals "" | verify-true
		save_enabled | verify-true
		
		//Test that 3 of a kind produces error
		add_component "Temperature controllers" $duplicate1
		add_component "Temperature controllers" $duplicate1
		
		error_text | contains $duplicate1 | verify-true
		save_enabled | verify-false
		
		//Test that removing one from 3 of a kind retains error
		//delete_component $duplicate1
		delete_component $duplicate1			
		error_text | contains $duplicate1 | verify-true
		save_enabled | verify-false
		
		//Test that fixing one duplicate doesn't remove error for other
		add_component "Power supplies" $duplicate2
		add_component "Power supplies" $duplicate2
		delete_component $duplicate1		
		
		error_text | contains $duplicate2 | verify-true
		save_enabled | verify-false
		
		delete_component $duplicate2
		delete_component $duplicate2
		
		error_text | equals "" | verify-true
		save_enabled | verify-true
		
		//Test that duplicates on different levels produces error
		get-tree | select $duplicate1
		add_component "Temperature controllers" $duplicate1
		
		error_text | contains $duplicate1 | verify-true
		save_enabled | verify-false
		
	}
}
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
