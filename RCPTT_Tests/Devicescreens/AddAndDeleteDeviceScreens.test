--- RCPTT testcase ---
Format-Version: 1.0
Element-Name: AddAndDeleteDeviceScreens
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _xCZlkAirEeewRLVbkB4NiA
Runtime-Version: 2.1.0.201606221726
Save-Time: 5/22/18 4:45 PM
Testcase-Type: ecl

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

// Test device screens can be added and deleted

switch_to_device_screens_view
let [ val device_screen_count [get-table | get-property "itemCount" -raw true]] 
	[val screen01 [test_prefix "test_screen_01"]] [val screen02 [test_prefix "test_screen_02"]] 
	[val screen03 [test_prefix "test_screen_03"]] {
		
	// Check that screen can't be added without a target
    with [edit_device_screens] {	    
    	get-button Add | check_button_enabled_and_click
        	
	    // Is there error text? Don't use the full text because it'll be fragile but I expect it should suggest the target
	    // as the problem    
    	get-editbox -after [get-label "Configure Device Screens"] | get-property text | contains "target" | verify-true
    	
    	// We shouldn't be able to press OK
		get-button OK | get-property enablement | equals false | verify-true
		
		check_button_enabled_and_click [get-button Cancel]
	}		
		
	// add screens
	with [edit_device_screens] {
		add_screen_with_target $screen01
		add_screen_with_target $screen02
		add_screen_with_target $screen03
	
	   	get-button OK | check_button_enabled_and_click
	}
		
	// verify screens have appeared
	try -times 10 -delay 200 - command {
		get-table | get-item $screen01 -column Name | get-property "columns[1]" | equals $screen01 | verify-true
		get-table | get-item $screen02 -column Name | get-property "columns[1]" | equals $screen02 | verify-true
		get-table | get-item $screen03 -column Name | get-property "columns[1]" | equals $screen03 | verify-true
	}
		
	// delete added screens	
	with [edit_device_screens] {
		with [device_screens_group] {
		    get-list | select $screen01
		    get-button Delete | click
		    get-list | select $screen02 $screen03
		    get-button Delete | click
		}
		get-button OK | click
	}
}
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
